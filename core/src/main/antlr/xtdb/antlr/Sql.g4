grammar Sql;

options {
  language = Java;
  caseInsensitive = true;
}

BLOCK_COMMENT : '/*' ( BLOCK_COMMENT | . )*? '*/'  -> skip ;
LINE_COMMENT : '--' ~[\r\n]* -> skip ;

PLUS : '+' ;
MINUS : '-' ;
ASTERISK : '*' ;
SOLIDUS : '/' ;

TRUE : 'TRUE' ;
FALSE : 'FALSE' ;
NULL : 'NULL' ;

fragment DIGIT : [0-9] ;
fragment LETTER : [a-z] ;
fragment HEX_DIGIT : [0-9a-f] ;
fragment SIGN : '+' | '-' ;

WHITESPACE : [ \n\r\t]+ -> skip ;

/// SQL Keywords

ALL: 'ALL' ;
DISTINCT: 'DISTINCT' ;
NOT: 'NOT' ;
ASYMMETRIC: 'ASYMMETRIC' ;
SYMMETRIC: 'SYMMETRIC' ;

/// §22 Direct invocation of SQL

/// §22.1 <direct SQL statement>

directSqlStatement : directlyExecutableStatement EOF ;

directlyExecutableStatement
    : queryExpression
    | insertStatement
    | updateStatementSearched
    | deleteStatementSearched
    | eraseStatementSearched
    | sqlTransactionStatement
    | sqlSessionStatement
    | setSessionVariableStatement
    | setValidTimeDefaults
    ;

setSessionVariableStatement : 'SET' identifier ( 'TO' | '=' ) literal ;
setValidTimeDefaults : 'SET' 'VALID_TIME_DEFAULTS' ( 'TO' | '=' ) ('AS_OF_NOW' | 'ISO_STANDARD') ;

//// §5 Lexical Elements

/// §5.3 Literals

literal
    : FLOAT #FloatLiteral
    | (SIGNED_INTEGER | UNSIGNED_INTEGER) #IntegerLiteral
    | characterString #CharacterStringLiteral
    | BINARY_STRING #BinaryStringLiteral
    | 'DATE' characterString #DateLiteral
    | 'TIME' characterString #TimeLiteral
    | 'TIMESTAMP' characterString #TimestampLiteral
    | 'INTERVAL' (PLUS | MINUS)? characterString intervalQualifier? #IntervalLiteral
    | 'DURATION' characterString #DurationLiteral
    | (TRUE | FALSE) #BooleanLiteral
    | NULL #NullLiteral
    ;

fragment EXPONENT : 'E' SIGN? DIGIT+ ;
FLOAT : SIGN? DIGIT+ ('.' DIGIT+ EXPONENT? | EXPONENT) ;
SIGNED_INTEGER : SIGN DIGIT+ ;
UNSIGNED_INTEGER : DIGIT+ ;

CHARACTER_STRING : '\'' ('\'\'' | ~('\''|'\n'))* '\'' ;
characterString : CHARACTER_STRING ;
BINARY_STRING : 'X(\'' HEX_DIGIT* '\')' ;

intervalQualifier : startField 'TO' endField | singleDatetimeField ;
startField : nonSecondPrimaryDatetimeField ;
endField : singleDatetimeField ;
intervalFractionalSecondsPrecision : UNSIGNED_INTEGER ;
nonSecondPrimaryDatetimeField : 'YEAR' | 'MONTH' | 'DAY' | 'HOUR' | 'MINUTE' ;
singleDatetimeField : nonSecondPrimaryDatetimeField | 'SECOND' ( '(' intervalFractionalSecondsPrecision ')' )? ;

/// §5.4 Identifiers

identifier
    : REGULAR_IDENTIFIER # RegularIdentifier
    | DELIMITED_IDENTIFIER # DelimitedIdentifier
    ;

REGULAR_IDENTIFIER : (LETTER | '$' | '_') (LETTER | DIGIT | '$' | '_' )* ;

DELIMITED_IDENTIFIER
    : '"' ('"' '"' | ~'"')* '"'
    | '`' ('`' '`' | ~'`')* '`'
    ;

schemaName : 'INFORMATION_SCHEMA' | 'PG_CATALOG' | 'PUBLIC' ;
tableName : (identifier | schemaName '.' identifier) ;
columnName : identifier ;
correlationName : identifier ;

queryName : identifier ;
fieldName : identifier ;
windowName : identifier ;

// §6 Scalar Expressions

/// §6.1 Data Types

dataType
    : ('NUMERIC' | 'DECIMAL' | 'DEC') ('(' precision (',' scale)? ')')? # DecimalType
    | ('SMALLINT' | 'INTEGER' | 'INT' | 'BIGINT') # IntegerType
    | 'FLOAT' ('(' precision ')')? # FloatType
    | 'REAL' # RealType
    | 'DOUBLE' 'PRECISION' # DoubleType
    | 'BOOLEAN' # BooleanType
    | 'DATE' # DateType
    | 'TIME' ('(' precision ')')? withOrWithoutTimeZone? # TimeType
    | 'TIMESTAMP' ('(' precision ')')? withOrWithoutTimeZone? # TimestampType
    | 'INTERVAL' intervalQualifier? # IntervalType
    | 'VARCHAR' # CharacterStringType
    | 'DURATION' ('(' precision ')')? # DurationType
    | 'ROW' '(' fieldDefinition (',' fieldDefinition)* ')' # RowType
    | dataType 'ARRAY' ('[' maximumCardinality ']')? # ArrayType
    ;

precision : UNSIGNED_INTEGER ;
scale : UNSIGNED_INTEGER ;

charLengthUnits : 'CHARACTERS' | 'OCTETS' ;

withOrWithoutTimeZone
    : 'WITH' 'TIME' 'ZONE' #WithTimeZone
    | 'WITHOUT' 'TIME' 'ZONE' #WithoutTimeZone
    ;

maximumCardinality : UNSIGNED_INTEGER ;

fieldDefinition : fieldName dataType ;

/// §6.3 <value expression primary>

expr
    : expr '.' fieldName #FieldAccess
    | expr '[' expr ']' #ArrayAccess
    | '+' expr #UnaryPlusExpr
    | '-' expr #UnaryMinusExpr
    | expr '*' expr #MultiplyExpr
    | expr '/' expr #DivideExpr
    | expr '+' expr #AddExpr
    | expr '-' expr #SubtractExpr
    | expr '||' expr #ConcatExpr
    | expr 'IS' NOT? booleanValue #IsBooleanValueExpr
    | expr predicatePart2 #PredicatePart2Expr
    | 'NOT' expr #UnaryNotExpr
    | expr 'AND' expr #AndExpr
    | expr 'OR' expr #OrExpr
    | '(' expr ')' #WrappedExpr

    | literal # LiteralExpr
    | parameterSpecification # ParamExpr
    | columnReference # ColumnExpr
    | aggregateFunction # AggregateFunctionExpr
    | windowFunctionType 'OVER' windowNameOrSpecification # WindowFunctionExpr
    | nestedWindowFunction # NestedWindowFunctionExpr
    | subquery # ScalarSubqueryExpr
    | 'NEST_ONE' subquery # NestOneSubqueryExpr
    | 'NEST_MANY' subquery # NestManySubqueryExpr
    | 'CASE' expr simpleWhenClause+ elseClause? 'END' #SimpleCaseExpr
    | 'CASE' searchedWhenClause+ elseClause? 'END' # SearchedCaseExpr
    | 'NULLIF' '(' expr ',' expr ')' # NullIfExpr
    | 'COALESCE' '(' expr (',' expr)* ')' # CoalesceExpr
    | 'CAST' '(' expr 'AS' dataType ')' # CastExpr
    | arrayValueConstructor # ArrayExpr
    | objectConstructor # ObjectExpr

    | 'EXISTS' subquery # ExistsPredicate

    // period predicates
    | periodPredicand 'OVERLAPS' periodPredicand # PeriodOverlapsPredicate
    | periodPredicand 'EQUALS' periodPredicand # PeriodEqualsPredicate
    | periodPredicand 'CONTAINS' periodOrPointInTimePredicand # PeriodContainsPredicate
    | periodPredicand 'PRECEDES' periodPredicand # PeriodPrecedesPredicate
    | periodPredicand 'SUCCEEDS' periodPredicand # PeriodSucceedsPredicate
    | periodPredicand 'IMMEDIATELY' 'PRECEDES' periodPredicand # PeriodImmediatelyPrecedesPredicate
    | periodPredicand 'IMMEDIATELY' 'SUCCEEDS' periodPredicand # PeriodImmediatelySucceedsPredicate

    | pgCatalogReference? 'HAS_TABLE_PRIVILEGE' '('
        ( userString ',' )?
        tableString ','
        privilegeString
      ')' # HasTablePrivilegePredicate

    | pgCatalogReference? 'HAS_SCHEMA_PRIVILEGE' '('
        ( userString ',' )?
        schemaString ','
        privilegeString
      ')' # HasSchemaPrivilegePredicate

    // numeric value functions
    | 'POSITION' '(' expr 'IN' expr ( 'USING' charLengthUnits )? ')' # PositionFunction
    | 'EXTRACT' '(' extractField 'FROM' extractSource ')' # ExtractFunction
    | ('CHAR_LENGTH' | 'CHARACTER_LENGTH') '(' expr ('USING' charLengthUnits)? ')' # CharacterLengthFunction
    | 'OCTET_LENGTH' '(' expr ')' # OctetLengthFunction
    | 'LENGTH' '(' expr ')' # LengthFunction
    | 'CARDINALITY' '(' expr ')' # CardinalityFunction
    | 'ABS' '(' expr ')' # AbsFunction
    | 'MOD' '(' expr ',' expr ')' # ModFunction
    | trigonometricFunctionName '(' expr ')' # TrigonometricFunction
    | 'LOG' '(' generalLogarithmBase ',' generalLogarithmArgument ')' # LogFunction
    | 'LOG10' '(' expr ')' # Log10Function
    | 'LN' '(' expr ')' # LnFunction
    | 'EXP' '(' expr ')' # ExpFunction
    | 'POWER' '(' expr ',' expr ')' # PowerFunction
    | 'SQRT' '(' expr ')' # SqrtFunction
    | 'FLOOR' '(' expr ')' # FloorFunction
    | ( 'CEIL' | 'CEILING' ) '(' expr ')' # CeilingFunction
    | 'LEAST' '(' expr (',' expr)* ')' # LeastFunction
    | 'GREATEST' '(' expr (',' expr)* ')' # GreatestFunction

    // string value functions
    | 'SUBSTRING' '('
        expr
        'FROM' startPosition
        ( 'FOR' stringLength )?
        ( 'USING' charLengthUnits )?
      ')' # CharacterSubstringFunction

    | 'UPPER' '(' expr ')' # UpperFunction
    | 'LOWER' '(' expr ')' # LowerFunction
    | 'TRIM' '(' trimSpecification? trimCharacter? 'FROM'? trimSource ')' # TrimFunction

    | 'OVERLAY' '('
        expr
        'PLACING' expr
        'FROM' startPosition
        ( 'FOR' stringLength )?
        ( 'USING' charLengthUnits )?
      ')' # OverlayFunction

    | 'CURRENT_USER' # CurrentUserFunction
    | 'CURRENT_SCHEMA' # CurrentSchemaFunction
    | 'CURRENT_DATABASE' # CurrentDatabaseFunction

    | 'CURRENT_DATE' ( '(' ')' )? # CurrentDateFunction
    | 'CURRENT_TIME' ('(' precision ')')? # CurrentTimeFunction
    | 'LOCALTIME' ('(' precision ')')? # LocalTimeFunction
    | 'CURRENT_TIMESTAMP' ('(' precision ')')? # CurrentTimestampFunction
    | 'LOCALTIMESTAMP' ('(' precision ')')? # LocalTimestampFunction
    | 'END_OF_TIME' ( '(' ')' )? # EndOfTimeFunction
    | 'DATE_TRUNC' '(' dateTruncPrecision ',' dateTruncSource (',' dateTruncTimeZone)? ')' # DateTruncFunction

    // interval value functions
    | 'AGE' '(' expr ',' expr ')' # AgeFunction

    | 'TRIM_ARRAY' '(' expr ',' expr ')' # TrimArrayFunction
    ;

booleanValue : 'TRUE' | 'FALSE' | 'UNKNOWN' ;

// spec addition: objectConstructor

objectConstructor
    : 'OBJECT' '(' (objectNameAndValue (',' objectNameAndValue)*)? ')'
    | '{' (objectNameAndValue (',' objectNameAndValue)*)? '}'
    ;

objectNameAndValue : objectName ':' expr ;

objectName : characterString ;

parameterSpecification
    : '?' #DynamicParameter
    | POSTGRES_PARAMETER_SPECIFICATION #PostgresParameter
    ;

POSTGRES_PARAMETER_SPECIFICATION : '$' DIGIT+ ;

columnReference : ((schemaName '.')? tableName '.')? columnName ;

/// §6.10 <window function>

windowFunctionType
    : rankFunctionType '(' ')' # RankWindowFunction
    | 'ROW_NUMBER' '(' ')' # RowNumberWindowFunction
    | aggregateFunction # AggregateWindowFunction
    | 'NTILE' '(' numberOfTiles ')' # NtileWindowFunction

    | ('LEAD' | 'LAG') '('
        leadOrLagExtent
        (',' offset (',' defaultExpression)?)?
      ')' (nullTreatment)? # LeadOrLagWindowFunction

    | firstOrLastValue '(' expr ')' nullTreatment? # FirstOrLastValueWindowFunction
    | 'NTH_VALUE' '(' expr ',' nthRow ')' fromFirstOrLast? nullTreatment? # NthValueWindowFunction
;

rankFunctionType : 'RANK' | 'DENSE_RANK' | 'PERCENT_RANK' | 'CUME_DIST' ;

numberOfTiles : UNSIGNED_INTEGER | parameterSpecification ;

leadOrLagExtent : expr ;
offset : UNSIGNED_INTEGER ;
defaultExpression : expr ;

nullTreatment
    : 'RESPECT' 'NULLS' # RespectNulls
    | 'IGNORE' 'NULLS' # IgnoreNulls
    ;

firstOrLastValue : 'FIRST_VALUE' | 'LAST_VALUE' ;

nthRow : UNSIGNED_INTEGER | '?' ;

fromFirstOrLast
    : 'FROM' 'FIRST' # FromFirst
    | 'FROM' 'LAST' # FromLast
    ;

windowNameOrSpecification : windowName | windowSpecification ;

/// §6.11 <nested window function>

nestedWindowFunction
    : 'ROW_NUMBER' '(' rowMarker ')' # NestedRowNumberFunction
    | 'VALUE_OF' '(' expr 'AT' rowMarkerExpression (',' valueOfDefaultValue)? ')' # ValueOfExprAtRow
    ;

rowMarker : 'BEGIN_PARTITION' | 'BEGIN_FRAME' | 'CURRENT_ROW' | 'FRAME_ROW' | 'END_FRAME' | 'END_PARTITION' ;

rowMarkerExpression : rowMarker rowMarkerDelta? ;

rowMarkerDelta : PLUS rowMarkerOffset | MINUS rowMarkerOffset ;

rowMarkerOffset : UNSIGNED_INTEGER | '?' ;
valueOfDefaultValue : expr ;

/// §6.12 <case expression>

simpleWhenClause : 'WHEN' whenOperandList 'THEN' expr ;
searchedWhenClause : 'WHEN' expr 'THEN' expr ;
elseClause : 'ELSE' expr ;

whenOperandList : whenOperand (',' whenOperand)* ;
whenOperand : predicatePart2 | expr ;

/// §6.28 <numeric value function>

extractField : primaryDatetimeField | timeZoneField ;
primaryDatetimeField : nonSecondPrimaryDatetimeField | 'SECOND' ;
timeZoneField : 'TIMEZONE_HOUR' | 'TIMEZONE_MINUTE' ;
extractSource : expr ;

/// §6.30 <string value function>

trimSource : expr ;
trimSpecification : 'LEADING' | 'TRAILING' | 'BOTH' ;
trimCharacter : expr ;

startPosition : expr ;
stringLength : expr ;

/// §6.32 <datetime value function>

// spec additions for date_trunc

dateTruncPrecision
    : 'YEAR' | 'QUARTER' | 'MONTH' | 'WEEK' | 'DAY'
    | 'HOUR' | 'MINUTE' | 'SECOND'
    | 'MILLISECOND' | 'MICROSECOND' | 'NANOSECOND'
    ;

dateTruncSource : expr ;
dateTruncTimeZone : characterString ;

/// §6.34 <interval value function>

// spec addition: age function

/// §6.37 <array value function>

/// §6.38 <array value constructor>

arrayValueConstructor
    : 'ARRAY'? '[' (expr (',' expr)*)? ']' # ArrayValueConstructorByEnumeration
    | 'ARRAY' subquery # ArrayValueConstructorByQuery
    ;

/// SQL:2016 §6.30 Trigonometric functions

trigonometricFunctionName : 'SIN' | 'COS' | 'TAN' | 'SINH' | 'COSH' | 'TANH' | 'ASIN' | 'ACOS' | 'ATAN' ;

// general_logarithm_function

generalLogarithmBase : expr ;
generalLogarithmArgument : expr ;

//// §7 Query expressions

/// §7.1 <row value constructor>

rowValueConstructor : '(' ( expr (',' expr)* )? ')' ;

/// §7.3 <table value constructor>

rowValueList : rowValueConstructor (',' rowValueConstructor)* ;
tableValueConstructor : 'VALUES' rowValueList ;

/// §7.5 <from clause>

fromClause : 'FROM' tableReference (',' tableReference)* ;

/// §7.6 <table reference>

tableReference
    : tableOrQueryName (querySystemTimePeriodSpecification | queryValidTimePeriodSpecification)* tableAlias? tableProjection? # BaseTable
    | tableReference joinType? 'JOIN' tableReference joinSpecification # JoinTable
    | tableReference 'CROSS' 'JOIN' tableReference # CrossJoinTable
    | tableReference 'NATURAL' joinType? 'JOIN' tableReference # NaturalJoinTable
    | subquery tableAlias tableProjection? # DerivedTable
    | 'LATERAL' subquery tableAlias tableProjection? # LateralDerivedTable
    | 'UNNEST' '(' expr ')' ('WITH' 'ORDINALITY')? tableAlias tableProjection? # CollectionDerivedTable
    | 'ARROW_TABLE' '(' characterString ')' tableAlias tableProjection? # ArrowTable
    | '(' tableReference ')' # WrappedTableReference
    ;

tableAlias : 'AS'? correlationName ;
tableProjection : '(' columnNameList ')' ;

querySystemTimePeriodSpecification
    : 'FOR' 'SYSTEM_TIME' tableTimePeriodSpecification
    | 'FOR' ALL 'SYSTEM_TIME'
    ;

queryValidTimePeriodSpecification
   : 'FOR' 'VALID_TIME' tableTimePeriodSpecification
   | 'FOR' ALL 'VALID_TIME'
   ;

tableTimePeriodSpecification
    : 'AS' 'OF' expr # TableAsOf
    | 'ALL' # TableAllTime
    | 'BETWEEN' expr 'AND' expr # TableBetween
    | 'FROM' expr 'TO' expr # TableFromTo
    ;

tableOrQueryName : tableName ;

columnNameList : columnName (',' columnName)* ;

/// §7.7 <joined table>

joinSpecification
    : 'ON' expr # JoinCondition
    | 'USING' '(' columnNameList ')' # NamedColumnsJoin
    ;

joinType : 'INNER' | outerJoinType 'OUTER'? ;
outerJoinType : 'LEFT' | 'RIGHT' | 'FULL' ;

/// §7.8 <where clause>

whereClause : 'WHERE' expr ;

/// §7.9 <group by clause>

groupByClause : 'GROUP' 'BY' (setQuantifier)? groupingElement (',' groupingElement)* ;

groupingElement
    : columnReference # OrdinaryGroupingSet
    | '(' ')' # EmptyGroupingSet
    ;

/// §7.10 <having clause>

havingClause : 'HAVING' expr ;

/// §7.11 <window clause>

windowClause : 'WINDOW' windowDefinitionList ;
windowDefinitionList : windowDefinition (',' windowDefinition)* ;
windowDefinition : newWindowName 'AS' windowSpecification ;
newWindowName : windowName ;

windowSpecification : '(' windowSpecificationDetails ')' ;

windowSpecificationDetails : (existingWindowName)? (windowPartitionClause)? (windowOrderClause)? (windowFrameClause)? ;

existingWindowName : windowName ;
windowPartitionClause : 'PARTITION' 'BY' windowPartitionColumnReferenceList ;
windowPartitionColumnReferenceList : windowPartitionColumnReference (',' windowPartitionColumnReference)* ;
windowPartitionColumnReference : columnReference ;
windowOrderClause : 'ORDER' 'BY' sortSpecificationList ;
windowFrameClause : windowFrameUnits windowFrameExtent (windowFrameExclusion)? ;
windowFrameUnits : 'ROWS' | 'RANGE' | 'GROUPS' ;
windowFrameExtent : windowFrameStart | windowFrameBetween ;
windowFrameStart : 'UNBOUNDED' 'PRECEDING' | windowFramePreceding | 'CURRENT' 'ROW' ;
windowFramePreceding : UNSIGNED_INTEGER 'PRECEDING' ;
windowFrameBetween : 'BETWEEN' windowFrameBound1 'AND' windowFrameBound2 ;
windowFrameBound1 : windowFrameBound ;
windowFrameBound2 : windowFrameBound ;
windowFrameBound : windowFrameStart | 'UNBOUNDED' 'FOLLOWING' | windowFrameFollowing ;
windowFrameFollowing : UNSIGNED_INTEGER 'FOLLOWING' ;

windowFrameExclusion : 'EXCLUDE' 'CURRENT' 'ROW' | 'EXCLUDE' 'GROUP' | 'EXCLUDE' 'TIES' | 'EXCLUDE' 'NO' 'OTHERS' ;

/// §7.12 <query specification>

selectClause : 'SELECT' setQuantifier? selectList ;
selectList : ASTERISK | selectSublist (',' selectSublist)* ;
selectSublist : derivedColumn | qualifiedAsterisk ;
qualifiedAsterisk : identifier '.' ASTERISK ;
derivedColumn : expr asClause? ;
asClause : 'AS'? columnName ;

/// §7.13 <query expression>

queryExpression : withClause? queryExpressionBody orderByClause? resultOffsetClause? fetchFirstClause? ;
withClause : 'WITH' 'RECURSIVE'? withListElement (',' withListElement)* ;
withListElement : queryName ('(' columnNameList ')')? 'AS' subquery ;

queryExpressionBody
    : queryExpressionBody 'UNION' (ALL | DISTINCT)? queryExpressionBody # UnionQuery
    | queryExpressionBody 'EXCEPT' (ALL | DISTINCT)? queryExpressionBody # ExceptQuery
    | queryExpressionBody 'INTERSECT' (ALL | DISTINCT)? queryExpressionBody # IntersectQuery
    | (selectClause fromClause? | fromClause) whereClause? groupByClause? havingClause? windowClause? # QuerySpecification
    | tableValueConstructor # ValuesQuery
    | '(' queryExpressionBody ')' # WrappedQuery
    ;

orderByClause : 'ORDER' 'BY' sortSpecificationList ;
resultOffsetClause : 'OFFSET' offsetRowCount ( 'ROW' | 'ROWS' )? ;

fetchFirstClause
    : 'FETCH' ('FIRST' | 'NEXT') fetchFirstRowCount? ( 'ROW' | 'ROWS' ) 'ONLY'
    | 'LIMIT' fetchFirstRowCount
    ;

offsetRowCount : UNSIGNED_INTEGER | parameterSpecification ;
fetchFirstRowCount : UNSIGNED_INTEGER | parameterSpecification ;

/// §7.15 <subquery>

subquery : '(' queryExpression ')' ;

//// §8 Predicates

predicatePart2
    : compOp expr # ComparisonPredicatePart2
    | NOT? 'BETWEEN' (ASYMMETRIC | SYMMETRIC)? expr 'AND' expr # BetweenPredicatePart2
    | NOT? 'IN' inPredicateValue # InPredicatePart2
    | 'NOT'? 'LIKE' likePattern ('ESCAPE' likeEscape)? # LikePredicatePart2
    | 'NOT'? 'LIKE_REGEX' xqueryPattern ('FLAG' xqueryOptionFlag)? # LikeRegexPredicatePart2
    | postgresRegexOperator xqueryPattern # PostgresRegexPredicatePart2
    | 'IS' 'NOT'? 'NULL' # NullPredicatePart2
    | compOp quantifier subquery # QuantifiedComparisonPredicatePart2
    ;

compOp : '=' | '!=' | '<>' | '<' | '>' | '<=' | '>=' ;

inPredicateValue
    : subquery # InSubquery
    | '(' rowValueList ')' # InRowValueList
    ;

likePattern : expr ;
likeEscape : expr ;

xqueryPattern : expr ;
xqueryOptionFlag : expr ;

postgresRegexOperator : '~' | '~*' | '!~' | '!~*' ;

quantifier : 'ALL' | 'SOME' | 'ANY' ;

periodPredicand
    : (tableName '.')? periodColumnName # PeriodColumnReference
    | 'PERIOD' '(' periodStartValue ',' periodEndValue ')' # PeriodValueConstructor
    ;

periodColumnName : 'VALID_TIME' | 'SYSTEM_TIME' ;
periodStartValue : expr ;
periodEndValue : expr ;

periodOrPointInTimePredicand : periodPredicand | pointInTimePredicand ;
pointInTimePredicand : expr ;
/// §8.21 <search condition>

searchCondition : expr ;

/// postgres access privilege predicates

pgCatalogReference : 'PG_CATALOG' '.' ;

userString : expr ;
tableString : expr ;
schemaString : expr ;
privilegeString : expr ;

//// §10 Additional common elements

/// §10.9 <aggregate function>

aggregateFunction
    : 'COUNT' '(' ASTERISK ')' # CountStarFunction
    | 'ARRAY_AGG' '(' expr ('ORDER' 'BY' sortSpecificationList)? ')' # ArrayAggFunction
    | setFunctionType '(' setQuantifier? expr ')' # SetFunction
    ;

setFunctionType
    : 'AVG' | 'MAX' | 'MIN' | 'SUM' | 'COUNT'
    | 'EVERY' | 'ANY' | 'SOME'
    | 'STDDEV_POP' | 'STDDEV_SAMP' | 'VAR_SAMP' | 'VAR_POP' ;

setQuantifier : 'DISTINCT' | 'ALL' ;

/// §10.10 <sort specification list>

sortSpecificationList : sortSpecification (',' sortSpecification)* ;
sortSpecification : expr orderingSpecification? nullOrdering? ;
orderingSpecification : 'ASC' | 'DESC' ;
nullOrdering : 'NULLS' 'FIRST' | 'NULLS' 'LAST' ;

/// §13.4 <SQL procedure statement>

sqlTransactionStatement
    : ('START' 'TRANSACTION' | 'BEGIN') transactionCharacteristics? # StartTransactionStatement
    | 'SET' 'LOCAL'? 'TRANSACTION' transactionCharacteristics # SetTransactionStatement
    | 'COMMIT' # CommitStatement
    | 'ROLLBACK' # RollbackStatement
    ;

sqlSessionStatement
    : 'SET' 'SESSION' 'CHARACTERISTICS' 'AS' sessionCharacteristicList # SetSessionCharacteristicsStatement
    | 'SET' 'TIME' 'ZONE' setTimeZoneValue # SetTimeZoneStatement
    ;

/// §14 Data manipulation

/// §14.9 <delete statement: searched>

deleteStatementSearched
    : 'DELETE' 'FROM' tableName
      dmlStatementValidTimeExtents?
      ( 'AS'? correlationName )?
      ( 'WHERE' searchCondition )?
    ;

dmlStatementValidTimeExtents : dmlStatementValidTimePortion | dmlStatementValidTimeAll ;
dmlStatementValidTimePortion : 'FOR' 'PORTION' 'OF' 'VALID_TIME' 'FROM' expr 'TO' expr ;
dmlStatementValidTimeAll : 'FOR' 'ALL' 'VALID_TIME';
eraseStatementSearched : 'ERASE' 'FROM' tableName ( 'AS'? correlationName )? ('WHERE' searchCondition)? ;

/// §14.11 <insert statement>

insertStatement : 'INSERT' 'INTO' tableName insertColumnsAndSource ;
insertColumnsAndSource
    : ( '(' columnNameList ')' ) tableValueConstructor # InsertValues
    | ( '(' columnNameList ')' )? queryExpression # InsertFromSubquery
    ;

/// §14.14 <update statement: searched>

updateStatementSearched
    : 'UPDATE' tableName
      dmlStatementValidTimeExtents?
      ( 'AS'? correlationName )?
      'SET' setClauseList
      ( 'WHERE' searchCondition )?
    ;

/// §14.15 <set clause list>

setClauseList : setClause (',' setClause)* ;
setClause : setTarget '=' updateSource ;
setTarget : objectColumn ('[' UNSIGNED_INTEGER ']')? ;

objectColumn : columnName ;

updateSource : expr ;

/// §17.3 <transaction characteristics>

transactionCharacteristics : transactionMode (',' transactionMode)* ;
transactionMode : isolationLevel | transactionAccessMode ;
transactionAccessMode
    : 'READ' 'ONLY' # ReadOnlyTransaction
    | 'READ' 'WRITE' # ReadWriteTransaction
    ;

isolationLevel : 'ISOLATION' 'LEVEL' levelOfIsolation ;

levelOfIsolation
    : 'READ' 'UNCOMMITTED' # ReadUncommittedIsolation
    | 'READ' 'COMMITTED' # ReadCommittedIsolation
    | 'REPEATABLE' 'READ' # RepeatableReadIsolation
    | 'SERIALIZABLE' # SerializableIsolation
    ;

/// §19.1 <set session characteristics statement>

sessionCharacteristicList : sessionCharacteristic (',' sessionCharacteristic)* ;
sessionCharacteristic : sessionTransactionCharacteristics ;
sessionTransactionCharacteristics : 'TRANSACTION' transactionMode (',' transactionMode)* ;

/// §19.4 <set local time zone statement>

setTimeZoneValue : characterString ;


